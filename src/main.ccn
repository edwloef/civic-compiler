start phase RootPhase {
    actions {
        pass scanparse;
        Analysis;
        Desugar;
        AstOpt;
        Free;
    }
};

traversal Print {
    uid = PRT
};

phase Stats {
    actions {
        traversal CountOps {
            uid = SCO,
            nodes = { BinOp },
            travdata {
                int add,
                int sub,
                int mul,
                int div,
                int mod
            }
        };

        traversal CountIdents {
            uid = SCI,
            nodes = { Id },
            travdata {
                user htable_st_ptr count
            }
        };
    }
};

phase Analysis {
    actions {
        traversal Functions {
            uid = AF,
            nodes = { Program, FunDecl, FunBody },
            travdata {
                user funtable_ptr funtable
            }
        };

        traversal Globals {
            uid = AG,
            nodes = { Program, VarDecl },
            travdata {
                user funtable_ptr funtable,
                user vartable_ptr vartable
            }
        };

        traversal Resolve {
            uid = AR,
            nodes = { Program, FunDecl, FunBody, Param, VarDecl, For, Call, VarRef },
            travdata {
                bool globals,
                user funtable_ptr funtable,
                user vartable_ptr vartable
            }
        };

        traversal TypeCheck {
            uid = ATC,
            nodes = ArrExpr | Stmt | { Program, FunDecl, FunBody, VarDecl },
            travdata {
                user funtable_ptr funtable,
                user vartable_ptr vartable,
                BasicType ret_ty
            }
        };

        traversal Diverge {
            uid = AD,
            nodes = { Program, Stmts, FunDecl, IfElse, While, DoWhile, Return }
        };
    }
};

phase Desugar {
    actions {
        traversal Casts {
            uid = DC,
            nodes = { Cast }
        };

        traversal TypeExprs {
            uid = DTE,
            nodes = { Program, Decls, FunDecl },
            travdata {
                user vartable_ptr vartable
            }
        };
    }
};

cycle AstOpt {
    actions {
        traversal ConstFold {
            uid = AOCF,
            nodes = { Monop, Binop, Cast }
        };

        cycle ComputeTransp {
            actions {
                traversal Transp {
                    uid = AOT,
                    nodes = ArrExpr | { Program, FunDecl, FunBody, Assign, Exprs },
                    travdata {
                        user funtable_ptr funtable,
                        user vartable_ptr vartable,
                        int level,
                        int min_level
                    }
                };
            }
        };

        traversal TrivialControlFlow {
            uid = AOTCF,
            nodes = { Stmts }
        };

        traversal Identities {
            uid = AOI,
            nodes = { Monop, Binop }
        };
    }
};

traversal Free {
    uid = F,
    nodes = { Program, Fundecl, Funbody }
};

enum BasicType {
    prefix = TY,
    values {
        bool, int, float, void, error
    }
};

enum MonOpKind {
    prefix = MO,
    values {
        pos, neg, not
    }
};

enum BinOpKind {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne, and, or
    }
};

root node Program {
    children {
        Decls decls { constructor, mandatory }
    },

    attributes {
        user funtable_ptr funtable,
        user vartable_ptr vartable
    }
};

node Decls {
    children {
        Decl decl { constructor, mandatory },
        Decls next { constructor }
    }
};

nodeset Decl = { FunDecl, VarDecl };

node Stmts {
    children {
        Stmt stmt { constructor, mandatory },
        Stmts next { constructor }
    },

    attributes {
        bool diverges
    }
};

nodeset Stmt {
    nodes = { Assign, IfElse, While, DoWhile, For, Return, Call },

    attributes {
        bool diverges
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    },

    attributes {
        bool transp
    }
};

nodeset Expr = { Monop, Binop, Cast, Call, VarRef, Int, Float, Bool };

node ArrExprs {
    children {
        ArrExpr expr { constructor, mandatory },
        ArrExprs next { constructor }
    }
};

nodeset ArrExpr {
    nodes = Expr | { ArrExprs },

    attributes {
        BasicType resolved_ty,
        int resolved_dims,
        bool transp
    }
};

node FunDecl {
    children {
        Id id { constructor, mandatory },
        Params params { constructor },
        FunBody body
    },

    attributes {
        BasicType ty { constructor },
        bool external,
        bool exported,
        user vartable_ptr vartable,
        int l
    }
};

node FunBody {
    children {
        Decls decls { constructor },
        Stmts stmts { constructor }
    },

    attributes {
        user funtable_ptr funtable
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params next { constructor }
    }
};

node Param {
    children {
        Type ty { constructor, mandatory },
        Id id { constructor, mandatory }
    }
};

node VarDecl {
    children {
        Type ty { constructor, mandatory },
        Id id { constructor, mandatory },
        ArrExpr expr { constructor }
    },

    attributes {
        bool exported,
        bool external,
        bool global,
        int l
    }
};

node Assign {
    children {
        VarRef ref { constructor, mandatory },
        ArrExpr expr { constructor, mandatory }
    }
};

node IfElse {
    children {
        Expr expr { constructor, mandatory },
        Stmts if_block { constructor },
        Stmts else_block { constructor }
    }
};

node While {
    children {
        Expr expr { constructor },
        Stmts stmts { constructor }
    }
};

node DoWhile {
    children {
        Stmts stmts { constructor },
        Expr expr { constructor }
    }
};

node For {
    children {
        Id id { constructor, mandatory },
        Expr loop_start { constructor, mandatory },
        Expr loop_end { constructor, mandatory },
        Expr loop_step { constructor, mandatory },
        Stmts stmts { constructor }
    }
};

node Return {
    children {
        Expr expr { constructor }
    }
};

node Monop {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        MonOpKind op { constructor }
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpKind op { constructor }
    }
};

node Cast {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        BasicType ty { constructor }
    }
};

node Call {
    children {
        Id id { constructor, mandatory },
        Exprs exprs { constructor }
    },

    attributes {
        int n,
        int l
    }
};

node VarRef {
    children {
        Id id { constructor, mandatory },
        Exprs exprs { constructor }
    },

    attributes {
        int n,
        int l
    }
};

node Id {
    attributes {
        string val { constructor, mandatory }
    }
};

node Type {
    children {
        Exprs exprs { constructor }
    },

    attributes {
        BasicType ty { constructor }
    }
};

node Int {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        float val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
