start phase RootPhase {
    actions {
        pass scanparse;
        Analysis;
        Desugar;
        AstOpt;
        Free;
    }
};

traversal Print {
    uid = PRT,
    travdata {
        int indent
    }
};

phase Stats {
    actions {
        traversal CountOps {
            uid = SCO,
            nodes = { BinOp },
            travdata {
                int add,
                int sub,
                int mul,
                int div,
                int mod
            }
        };

        traversal CountIdents {
            uid = SCI,
            nodes = { Id },
            travdata {
                user htable_st_ptr count
            }
        };
    }
};

phase Analysis {
    actions {
        traversal Functions {
            uid = AF,
            nodes = { Program, FunDecl, FunBody },
            travdata {
                user funtable_ptr funtable
            }
        };

        traversal Globals {
            uid = AG,
            nodes = { Program, VarDecl },
            travdata {
                user funtable_ptr funtable,
                user vartable_ptr vartable
            }
        };

        traversal Resolve {
            uid = AR,
            nodes = { Program, FunDecl, FunBody, Param, VarDecl, For, Call, VarRef },
            travdata {
                bool globals,
                user funtable_ptr funtable,
                user vartable_ptr vartable
            }
        };

        traversal TypeCheck {
            uid = ATC,
            nodes = Expr | Stmt | { Program, FunDecl, FunBody, VarDecl },
            travdata {
                user funtable_ptr funtable,
                user vartable_ptr vartable,
                BasicType ret_ty
            }
        };

        traversal Divergence {
            uid = AD,
            nodes = { Program, Stmts, FunDecl, IfElse, While, DoWhile, Return }
        };
    }
};

phase Desugar {
    actions {
        traversal BoolCasts {
            uid = DBC,
            nodes = { Cast }
        };

        traversal DelWhile {
            uid = DDW,
            nodes = { While }
        };

        traversal VarDecls {
            uid = DVD,
            nodes = { Program, FunDecl, FunBody, Decls, VarRef, Call },
            travdata {
                user vartable_ptr vartable,
                Stmts stmts
            }
        };

        traversal ArrayAssign {
            uid = DAA,
            nodes = { Program, FunDecl, Stmts, Assign, ArrExprs },
            travdata {
                user vartable_ptr vartable,
                Stmts stmts
            }
        };

        traversal DimReduce {
            uid = DDR,
            nodes = { Program, FunDecl, VarRef, Malloc },
            travdata {
                user vartable_ptr vartable
            }
        };
    }
};

cycle AstOpt {
    actions {
        traversal CountWrites {
            uid = CW,
            nodes = { Program, FunDecl, VarRef },
            travdata {
                user vartable_ptr vartable
            }
        };

        traversal CopyProp {
            uid = AOCP,
            nodes = { Program, FunDecl, Stmts },
            travdata {
                user vartable_ptr vartable
            }
        };

        traversal Reassoc {
            uid = AOR,
            nodes = { Binop }
        };

        traversal ConstFold {
            uid = AOCF,
            nodes = { Monop, Binop, Cast }
        };

        traversal TrivialControlFlow {
            uid = AOTCF,
            nodes = { Stmts }
        };

        traversal Identities {
            uid = AOI,
            nodes = { Monop, Binop, Cast }
        };
    }
};

traversal PropValue {
    uid = PV,
    nodes = Stmt | { Stmts, VarRef },
    travdata {
        Expr expr,
        int write_count,
        int n,
        int l
    }
};

traversal PropIntoLoop {
    uid = PIL,
    nodes = { VarRef },
    travdata {
        Expr expr,
        int n,
        int l
    }
};

traversal CheckProp {
    uid = CP,
    nodes = { Call, VarRef },
    travdata {
        bool can_prop,
        int write_count,
        int n,
        int l
    }
};

traversal Free {
    uid = F,
    nodes = { Program, Fundecl, Funbody }
};

enum BasicType {
    prefix = TY,
    values {
        bool, int, float, void, error
    }
};

enum MonOpKind {
    prefix = MO,
    values {
        pos, neg, not
    }
};

enum BinOpKind {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne, and, or
    }
};

root node Program {
    children {
        Decls decls { constructor, mandatory }
    },

    attributes {
        user funtable_ptr funtable,
        user vartable_ptr vartable
    }
};

node Decls {
    children {
        Decl decl { constructor, mandatory },
        Decls next { constructor }
    }
};

nodeset Decl = { FunDecl, VarDecl };

node Stmts {
    children {
        Stmt stmt { constructor, mandatory },
        Stmts next { constructor }
    },

    attributes {
        bool diverges
    }
};

nodeset Stmt {
    nodes = { Assign, IfElse, While, DoWhile, For, Return, Call },

    attributes {
        bool diverges
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    },

    attributes {
        bool transp
    }
};

nodeset Expr {
    nodes = { ArrExprs, Monop, Binop, Cast, Call, VarRef, Int, Float, Bool, Malloc },

    attributes {
        BasicType resolved_ty,
        int resolved_dims,
        bool transp
    }
};

node ArrExprs {
    children {
        Expr expr { constructor, mandatory },
        ArrExprs next { constructor }
    }
};

node Malloc {
    children {
        Exprs exprs { constructor, mandatory }
    },

    lifetime {
        allowed (Desugar.VarDecls -> )
    }
};

node FunDecl {
    children {
        Id id { constructor, mandatory },
        Params params { constructor },
        FunBody body
    },

    attributes {
        BasicType ty { constructor },
        bool external,
        bool exported,
        user vartable_ptr vartable,
        int l
    }
};

node FunBody {
    children {
        Decls decls { constructor },
        Stmts stmts { constructor }
    },

    attributes {
        user funtable_ptr funtable
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params next { constructor }
    }
};

node Param {
    children {
        Type ty { constructor, mandatory },
        Id id { constructor, mandatory }
    }
};

node VarDecl {
    children {
        Type ty { constructor, mandatory },
        Id id { constructor, mandatory },
        Expr expr { constructor }
    },

    attributes {
        bool exported,
        bool external,
        bool global,
        int l
    },

    lifetime {
        allowed ( -> Desugar.VarDecls]
    }
};

node Assign {
    children {
        VarRef ref { constructor, mandatory },
        Expr expr { constructor, mandatory }
    }
};

node IfElse {
    children {
        Expr expr { constructor, mandatory },
        Stmts if_block { constructor },
        Stmts else_block { constructor }
    }
};

node While {
    children {
        Expr expr { constructor },
        Stmts stmts { constructor }
    },

    lifetime {
        allowed ( -> Desugar.DelWhile]
    }
};

node DoWhile {
    children {
        Stmts stmts { constructor },
        Expr expr { constructor }
    }
};

node For {
    children {
        Id id { constructor, mandatory },
        Expr loop_start { constructor, mandatory },
        Expr loop_end { constructor, mandatory },
        Expr loop_step { constructor, mandatory },
        Stmts stmts { constructor }
    }
};

node Return {
    children {
        Expr expr { constructor }
    }
};

node Monop {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        MonOpKind op { constructor }
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpKind op { constructor }
    }
};

node Cast {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        BasicType ty { constructor }
    }
};

node Call {
    children {
        Id id { constructor, mandatory },
        Exprs exprs { constructor }
    },

    attributes {
        int n,
        int l
    }
};

node VarRef {
    children {
        Id id { constructor, mandatory },
        Exprs exprs { constructor }
    },

    attributes {
        bool write,
        int n,
        int l
    }
};

node Id {
    attributes {
        string val { constructor, mandatory }
    }
};

node Type {
    children {
        Exprs exprs { constructor }
    },

    attributes {
        BasicType ty { constructor }
    }
};

node Int {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        double val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
